# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# Copyright (c) 2022 Salvydas Lukosius

builtin emulate -L zsh ${=${options[xtrace]:#off}:+-o xtrace}
builtin setopt extended_glob warn_create_global typeset_silent

# Checking the cached .zcompdump file to see if it must be regenerated adds a noticable delay to zsh startup.
# This restricts checking it once a day, determines when to regenerate, as compinit doesn't always need to
# modify the compdump and compiles mapped to share (total mem reduction) run in background in multiple shells.

autoload -Uz compinit
typeset zcompf="${ZI[ZCOMPDUMP_PATH]:-${XDG_DATA_HOME:-$ZDOTDIR:-$HOME}/.cache/zi/.zcompdump}"
typeset zcompf_a="${zcompf}.augur"

# Globbing (#qN.mh+24):
# - '#q' is an explicit glob qualifier that makes globbing work within zsh's [[ ]] construct.
# - 'N' makes the glob pattern evaluate to nothing when it doesn't match (rather than throw a globbing error)
# - '.' matches "regular files"
# - 'mh+24' matches files, directories and etc., that are older than 24 hours.

if [[ -e "$zcompf_a" && -f "$zcompf_a"(#qN.mh+24) ]]; then
  compinit -i -d "$zcompf"
  command touch "$zcompf_a"
else
  compinit -C -d "$zcompf"
fi

# if .zcompdump exists (and is non-zero), and is older than the .zwc file, then regenerate
if [[ -s "$zcompf" && (! -s "${zcompf}.zwc" || "$zcompf" -nt "${zcompf}.zwc") ]]; then
  # since file is mapped, it might be mapped right now (current shells), so rename it then make a new one
  [[ -e "$zcompf.zwc" ]] && command mv -f "$zcompf.zwc" "$zcompf.zwc.old"

  # compile it mapped, so multiple shells can share it (total mem reduction) run in background
  { zcompile -M "$zcompf" && command rm -f "$zcompf.zwc.old" }&!
fi

unset zcompf
